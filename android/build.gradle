group = 'com.cleveradssolutions.plugin.flutter'
version = '0.8.7'

buildscript {
    ext.kotlin_version = '1.8.22'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.7.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            name = "MintegralAdsRepo"
            url = "https://dl-maven-android.mintegral.com/repository/mbridge_android_sdk_oversea"
            content { it.includeGroup("com.mbridge.msdk.oversea") }
        }
        maven {
            name = "PangleAdsRepo"
            url = "https://artifact.bytedance.com/repository/pangle"
            content { it.includeGroup("com.pangle.global") }
        }
        maven {
            name = "ChartboostAdsRepo"
            url = "https://cboost.jfrog.io/artifactory/chartboost-ads/"
            content { it.includeGroup("com.chartboost") }
        }
        maven {
            name = "YSONetworkRepo"
            url = "https://ysonetwork.s3.eu-west-3.amazonaws.com/sdk/android"
            content { it.includeGroup("com.ysocorp") }
        }
        maven {
            name = "OguryAdsRepo"
            url = "https://maven.ogury.co"
            content {
                it.includeGroup("co.ogury")
                it.includeGroup("co.ogury.module")
            }
        }
        maven {
            name = "MadexAdsRepo"
            url = "https://sdkpkg.sspnet.tech"
            content {
                it.includeGroup("sspnet.tech")
                it.includeGroup("sspnet.tech.adapters")
            }
        }
        maven {
            name = "SmaatoAdsRepo"
            url = "https://s3.amazonaws.com/smaato-sdk-releases/"
            content { it.includeGroup("com.smaato.android.sdk") }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace 'com.cleveradssolutions.plugin.flutter'
    compileSdk 34

    defaultConfig {
        minSdk 23
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation 'com.cleveradssolutions:cas-sdk:4.2.0'
}